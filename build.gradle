/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.github.jk1.dependency-license-report' version '2.1'
    id 'co.riiid.gradle' version '0.4.2'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: "com.github.jk1.dependency-license-report"

group = 'cd.go.gocd'
version = '1.0.0'

project.ext.projectDesc = [
  id         : 'cd.go.gocd.gocd-database-migrator',
  repo       : 'gocd-database-migrator',
  version    : project.version,
  name       : 'GoCD Database Migrator',
  description: 'Tool to help migrator pre-20.5.0 databases to 20.5.0 database.',
  vendorName : 'GoCD Contributors',
  vendorUrl  : 'https://github.com/gocd/gocd-database-migrator'
]


repositories {
    mavenCentral()
    maven { url "https://nexus.gocd.io/repository/maven-releases/" }
}

dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.9.0'

    implementation group: 'com.beust', name: 'jcommander', version: '1.82'
    implementation group: 'me.tongfei', name: 'progressbar', version: '0.9.4'
    implementation group: 'org.jooq', name: 'jooq', version: '3.13.1'

    implementation group: 'org.liquibase', name: 'liquibase-core', version: '3.8.9'
    implementation group: 'net.sf', name: 'dbdeploy', version: '2.11.1'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'
    implementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.2'

    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: "5.8.2"
    testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "5.8.2"
}

targetCompatibility = 1.8
sourceCompatibility = 1.8

mainClassName = 'com.thoughtworks.go.dbsync.cli.Main'
applicationDefaultJvmArgs = ['-Xmx4g']

tasks.withType(AbstractArchiveTask) {
    reproducibleFileOrder = true
    preserveFileTimestamps = false
}


test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(['Main-Class': mainClassName])
    }
}

distributions {
    main {
        contents {
            from("LICENSE")
            from("README.md")

            from(generateLicenseReport) {
                into "licenses"
            }

            from(file('LICENSE')) {
                into "licenses"
            }

            from("src/main/resources/h2deltas") {
                into "h2deltas"
            }

            from("src/main/resources/pgdeltas") {
                into "pgdeltas"
            }
        }
    }
}

distTar {
    compression = Compression.GZIP
}

tasks.withType(AbstractArchiveTask) { archiveTask ->
    includeEmptyDirs false
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    preserveFileTimestamps = false
    reproducibleFileOrder = true

    ['MD5', 'SHA1', 'SHA-256'].each { algo ->
        archiveTask.outputs.files("${archiveTask.archivePath}.${algo}")
        archiveTask.doLast {
            ant.checksum file: archiveTask.archivePath, format: 'MD5SUM', algorithm: algo
        }
    }
}

apply from: 'plugin-tasks.gradle'
