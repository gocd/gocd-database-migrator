/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'application'
    id 'com.github.jk1.dependency-license-report' version '2.5'
    id 'io.github.humblerookie.gradle' version '0.5.0'
}

group = 'cd.go.gocd'
version = '1.0.4'

project.ext.projectDesc = [
  id         : 'cd.go.gocd.gocd-database-migrator',
  repo       : 'gocd-database-migrator',
  version    : project.version,
  name       : 'GoCD Database Migrator',
  description: 'Tool to help migrator pre-20.5.0 databases to 20.5.0 database.',
  vendorName : 'GoCD Contributors',
  vendorUrl  : 'https://github.com/gocd/gocd-database-migrator'
]


repositories {
    mavenCentral()
    maven { url "https://nexus.gocd.io/repository/maven-releases/" }
}

dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.12.0'

    implementation group: 'com.beust', name: 'jcommander', version: '1.82'
    implementation group: 'me.tongfei', name: 'progressbar', version: '0.10.0'
    implementation group: 'org.jooq', name: 'jooq', version: '3.16.23'

    implementation group: 'org.liquibase', name: 'liquibase-core', version: '3.10.3'
    implementation group: 'net.sf', name: 'dbdeploy', version: '2.11.1'
    implementation group: 'com.mysql', name: 'mysql-connector-j', version: '8.3.0'
    implementation group: 'com.h2database', name: 'h2', version: '1.4.200' // Should match version at GoCD 20.5.0 release, since H2 dont guarantee backward compatiiblity
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.2'

    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api"
    testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine"
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

application {
    mainClass = 'com.thoughtworks.go.dbsync.cli.Main'
    applicationDefaultJvmArgs = ['-Xmx4g']
}

tasks.withType(AbstractArchiveTask) {
    reproducibleFileOrder = true
    preserveFileTimestamps = false
}


test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(['Main-Class': application.mainClass.get()])
    }
}

distributions {
    main {
        contents {
            from("LICENSE")
            from("README.md")

            from(generateLicenseReport) {
                into "licenses"
            }

            from(file('LICENSE')) {
                into "licenses"
            }

            from("src/main/resources/h2deltas") {
                into "h2deltas"
            }

            from("src/main/resources/pgdeltas") {
                into "pgdeltas"
            }
        }
    }
}

distTar {
    compression = Compression.GZIP
}

tasks.withType(AbstractArchiveTask) { archiveTask ->
    includeEmptyDirs false
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    preserveFileTimestamps = false
    reproducibleFileOrder = true

    ['MD5', 'SHA1', 'SHA-256'].each { algo ->
        archiveTask.outputs.files("${archiveTask.archiveFile.get()}.${algo}")
        archiveTask.doLast {
            ant.checksum file: archiveTask.archiveFile.get(), format: 'MD5SUM', algorithm: algo
        }
    }
}

apply from: 'plugin-tasks.gradle'
